import { set_e, mkdir, make, nasm, dd, dd_copy, mkfs_fat, mcopy } from "./utils.ab"

pub fun build(use_image: Text) {
    set_e()

    let build_directories = ["binaries", "binaries/user", "binaries/user/bin", "images"]
    let dir_bootloader = "bootloader"
    let bootloader_files = ["bootsect", "stage2"]
    let dir_kernel = "kernel"
    let dir_benlibc = "benlibc"
    let dir_user = "user"
    let user_programs = ["bin/ls", "bin/cash", "bin/echo", "bin/info", "bin/show", "bin/touch", "bin/write",
                         "bin/mkdir", "bin/rm"]

    for d in build_directories {
        mkdir(d)
    }

    make(dir_kernel)
    make(dir_benlibc)

    for p in user_programs {
        make("{dir_user}/{p}")
    }

    for f in bootloader_files {
        nasm("binaries/{f}.bin", "{dir_bootloader}/{f}.asm")
    }

    if (use_image == "") {
        dd("/dev/zero", "images/benix.img", 512, 2880)
        mkfs_fat(12, "images/benix.img")

        dd_copy("binaries/bootsect.bin", "images/benix.img", 0)
        dd_copy("binaries/stage2.bin", "images/benix.img", 1)
        dd_copy("binaries/kernel.bin", "images/benix.img", 2)

        mcopy("images/benix.img", "{dir_user}/README.txt", "::")
        mcopy("images/benix.img", "{dir_user}/LICENSE.txt", "::")
        mcopy("images/benix.img", "{dir_user}/sys", "::")
        mcopy("images/benix.img", "{dir_user}/home", "::")
        mcopy("images/benix.img", "binaries/user/bin", "::")
    }
}

main(args) {
    build(args[0])
}